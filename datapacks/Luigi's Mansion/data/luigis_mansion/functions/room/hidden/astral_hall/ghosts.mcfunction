execute if score #astral_hall Wave matches 3 unless entity @e[tag=ghost,scores={Room=45},tag=!optional_ghost] run function luigis_mansion:room/hidden/astral_hall/clear
execute if score #astral_hall Wave matches 2 unless entity @e[tag=ghost,scores={Room=45},tag=!optional_ghost] run function luigis_mansion:room/hidden/astral_hall/wave_3
execute if score #astral_hall Wave matches 1 unless entity @e[tag=ghost,scores={Room=45},tag=!optional_ghost] run function luigis_mansion:room/hidden/astral_hall/wave_2
execute as @a[x=666.5,y=20.5,z=97.5,distance=..1,gamemode=!spectator,limit=1] unless data storage luigis_mansion:data current_state.current_data.rooms.astral_hall{cleared:1b} if block 666 20 88 minecraft:air if block 665 22 93 minecraft:brewing_stand[has_bottle_0=true,has_bottle_1=true,has_bottle_2=true] if block 669 22 95 minecraft:brewing_stand[has_bottle_0=true,has_bottle_1=true,has_bottle_2=true] if block 669 22 99 minecraft:brewing_stand[has_bottle_0=true,has_bottle_1=true,has_bottle_2=true] if block 665 22 101 minecraft:brewing_stand[has_bottle_0=true,has_bottle_1=true,has_bottle_2=true] if block 662 22 97 minecraft:brewing_stand[has_bottle_0=true,has_bottle_1=true,has_bottle_2=true] unless entity @a[tag=astral_hall_ambush,limit=1] run tag @s add astral_hall_ambush
execute if data storage luigis_mansion:data current_state.current_data{blackout:1b} unless score #astral_hall Wave matches 1.. run function luigis_mansion:room/hidden/astral_hall/blackout