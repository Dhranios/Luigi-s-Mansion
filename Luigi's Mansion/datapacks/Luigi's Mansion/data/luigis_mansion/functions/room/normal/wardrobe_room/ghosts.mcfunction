execute as @e[tag=ghost,tag=hidden,scores={Room=5}] at @s unless block ~ ~ ~ #luigis_mansion:ghosts_ignore run tag @s add ignore_until_last_wave
execute if score #wardrobe_room Wave matches 3 unless entity @e[tag=ghost,scores={Room=5},tag=!optional_ghost] run function luigis_mansion:room/normal/wardrobe_room/clear
execute if score #wardrobe_room Wave matches 2 unless entity @e[tag=ghost,scores={Room=5},tag=!ignore_until_last_wave,tag=!optional_ghost] run function luigis_mansion:room/normal/wardrobe_room/wave_3
execute if score #wardrobe_room Wave matches 1 unless entity @e[tag=ghost,scores={Room=5},tag=!ignore_until_last_wave,tag=!optional_ghost] run function luigis_mansion:room/normal/wardrobe_room/wave_2
execute unless data storage luigis_mansion:data current_state.current_data.rooms.wardrobe_room{cleared:1b} if data storage luigis_mansion:data current_state.current_data{blackout:0b} unless score #wardrobe_room Wave matches 1..99 run function luigis_mansion:room/normal/wardrobe_room/wave_1
execute if score #wardrobe_room Wave matches 100 unless entity @e[tag=ghost,scores={Room=5},tag=!optional_ghost] run function luigis_mansion:room/normal/wardrobe_room/blackout_clear
execute if data storage luigis_mansion:data current_state.current_data{blackout:1b} unless score #wardrobe_room Wave matches 1.. run function luigis_mansion:room/normal/wardrobe_room/blackout