data modify storage luigis_mansion:data macro set value {namespace:"luigis_mansion",id:"gold_coin",uuid:[I;]}
data modify storage luigis_mansion:data macro.uuid set from entity @s UUID
function luigis_mansion:selection_menu/game_boy_horror/show_inventory/check_money with storage luigis_mansion:data macro
execute if score #temp Money matches 0.. run data modify storage luigis_mansion:data menu_options.options append value {id:"minecraft:diamond_pickaxe",Count:1b,tag:{Damage:22,Unbreakable:1b,CustomModelData:8,HideFlags:63,AttributeModifiers:[],display:{Name:'{"italic":false,"color":"white","type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_inventory.entry","with":[{"type":"translatable","translate":"luigis_mansion:item.gold_coin"},{"type":"text","text":"0"},{"type":"text","text":"0"}]}'},luigis_mansion:{option:{id:"reload"}}}}
execute if score #temp Money matches 0.. in minecraft:overworld run setblock 27 0 0 minecraft:oak_sign{front_text:{messages:['{"italic":false,"color":"white","type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_inventory.entry","with":[{"type":"translatable","translate":"luigis_mansion:item.gold_coin"},{"type":"score","score":{"objective":"Money","name":"#temp2"}},{"type":"score","score":{"objective":"Money","name":"#temp"}}]}','{"type":"text","text":""}','{"type":"text","text":""}','{"type":"text","text":""}']}}
execute if score #temp Money matches 0.. in minecraft:overworld run data modify storage luigis_mansion:data menu_options.options[-1].tag.display.Name set from block 27 0 0 front_text.messages[0]
execute if score #temp Money matches 0.. in minecraft:overworld run setblock 27 0 0 minecraft:bedrock
scoreboard players reset #temp Money
scoreboard players reset #temp2 Money