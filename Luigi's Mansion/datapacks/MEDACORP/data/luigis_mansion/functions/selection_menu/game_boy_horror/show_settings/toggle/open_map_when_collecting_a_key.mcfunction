tag @s[tag=!stop_map_on_key_collect] add toggle_stop_map_on_key_collect
tag @s[tag=toggle_stop_map_on_key_collect] add stop_map_on_key_collect
tag @s[tag=!toggle_stop_map_on_key_collect,tag=stop_map_on_key_collect] remove stop_map_on_key_collect
tag @s[tag=toggle_stop_map_on_key_collect] remove toggle_stop_map_on_key_collect

function luigis_mansion:entities/player/memory/get with entity @s

data modify storage luigis_mansion:data menu_options set from storage luigis_mansion:data my_memory.selection_menu.current_options

execute if entity @s[tag=!stop_map_on_key_collect] run data modify storage luigis_mansion:data menu_options.options[{components:{"minecraft:custom_data":{option:{id:"show_settings/toggle/open_map_when_collecting_a_key",namespace:"luigis_mansion"}}}}] set value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:damage":22,"minecraft:unbreakable":{show_in_tooltip:0b},"minecraft:custom_model_data":60,"minecraft:attribute_modifiers":{modifiers:[]},"minecraft:item_name":'{"type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_settings.value","with":[{"type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_settings.open_map_when_collecting_a_key"},{"type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_settings.value.on"}]}',"minecraft:custom_data":{option:{id:"show_settings/toggle/open_map_when_collecting_a_key",namespace:"luigis_mansion"}}}}
execute if entity @s[tag=stop_map_on_key_collect] run data modify storage luigis_mansion:data menu_options.options[{components:{"minecraft:custom_data":{option:{id:"show_settings/toggle/open_map_when_collecting_a_key",namespace:"luigis_mansion"}}}}] set value {id:"minecraft:diamond_pickaxe",count:1,components:{"minecraft:damage":22,"minecraft:unbreakable":{show_in_tooltip:0b},"minecraft:custom_model_data":59,"minecraft:attribute_modifiers":{modifiers:[]},"minecraft:item_name":'{"type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_settings.value","with":[{"type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_settings.open_map_when_collecting_a_key"},{"type":"translatable","translate":"luigis_mansion:item.menu.game_boy_horror.show_settings.value.off"}]}',"minecraft:custom_data":{option:{id:"show_settings/toggle/open_map_when_collecting_a_key",namespace:"luigis_mansion"}}}}

function luigis_mansion:entities/player/selection_menu/load