execute unless score #the_twins_room Time matches 1.. if entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] if score #players Totals matches 1 run tellraw @a {"type":"translatable","translate":"chat.type.text","with":[{"type":"translatable","translate":"luigis_mansion:entity.henry","color":"green"},{"type":"translatable","translate":"luigis_mansion:dialog.twins.game.3"}]}
execute unless score #the_twins_room Time matches 1.. if entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] if score #players Totals matches 2.. run tellraw @a {"type":"translatable","translate":"chat.type.text","with":[{"type":"translatable","translate":"luigis_mansion:entity.henry","color":"green"},{"type":"translatable","translate":"luigis_mansion:dialog.twins.game.3.more"}]}
execute if entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] run data modify storage luigis_mansion:data entity set value {room:49,scan_message:{sender:"me",message:'{"type":"translatable","translate":"luigis_mansion:message.henry.scan"}'},tags:["hide_and_seek","appear"],room_search:{non_players:1b}}
execute unless score #the_twins_room Time matches 1.. run data modify storage luigis_mansion:data entity.tags append value "next_wave"
execute if entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] rotated -90 0 run function luigis_mansion:spawn_entities/portrait_ghost/henry_and_orville/henry

execute unless score #the_twins_room Time matches 1.. unless entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] if score #players Totals matches 1 run tellraw @a {"type":"translatable","translate":"chat.type.text","with":[{"type":"translatable","translate":"luigis_mansion:entity.orville","color":"green"},{"type":"translatable","translate":"luigis_mansion:dialog.twins.game.2"}]}
execute unless score #the_twins_room Time matches 1.. unless entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] if score #players Totals matches 2.. run tellraw @a {"type":"translatable","translate":"chat.type.text","with":[{"type":"translatable","translate":"luigis_mansion:entity.orville","color":"green"},{"type":"translatable","translate":"luigis_mansion:dialog.twins.game.2.more"}]}
execute unless entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] run data modify storage luigis_mansion:data entity set value {room:49,scan_message:{sender:"me",message:'{"type":"translatable","translate":"luigis_mansion:message.orville.scan"}'},tags:["hide_and_seek","appear"],room_search:{non_players:1b}}
execute unless entity @e[nbt={data:{entity:{namespace:"luigis_mansion",id:"orville"}}},limit=1] rotated -90 0 run function luigis_mansion:spawn_entities/portrait_ghost/henry_and_orville/orville

execute unless score #the_twins_room Time matches 1.. run playsound luigis_mansion:music.solve_puzzle music @a[scores={Room=49}] ~ ~ ~ 10000
execute unless score #the_twins_room Time matches 1.. run scoreboard players set @a[scores={Room=49,Music=..30}] Music 30