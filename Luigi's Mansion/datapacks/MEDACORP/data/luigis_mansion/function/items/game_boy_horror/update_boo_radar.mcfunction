tag @e[tag=caught_by_boo_radar,tag=same_room] add radar_detect
execute unless data storage luigis_mansion:data current_state.current_data.obtained_items{boo_radar:1b} if entity @s[nbt=!{Inventory:[{components:{"minecraft:custom_model_data":{strings:["off"]},"minecraft:custom_data":{namespace:"luigis_mansion",id:"game_boy_horror"}}}]}] run function luigis_mansion:items/game_boy_horror/turn_radar_to_value {value:"off"}
execute if data storage luigis_mansion:data current_state.current_data.obtained_items{boo_radar:1b} unless entity @e[tag=radar_detect,limit=1] if entity @s[nbt=!{Inventory:[{components:{"minecraft:custom_model_data":{strings:["on"]},"minecraft:custom_data":{namespace:"luigis_mansion",id:"game_boy_horror"}}}]}] run function luigis_mansion:items/game_boy_horror/turn_radar_to_value {value:"on"}
execute if data storage luigis_mansion:data current_state.current_data.obtained_items{boo_radar:1b} if entity @e[tag=radar_detect,limit=1] run function luigis_mansion:items/game_boy_horror/trigger_radar
execute if entity @s[nbt={Inventory:[{components:{"minecraft:custom_model_data":{strings:["clock"]},"minecraft:custom_data":{namespace:"luigis_mansion",id:"game_boy_horror"}}}]}] run function luigis_mansion:items/game_boy_horror/clock_ghost_radar
tag @e[tag=radar_detect] remove radar_detect