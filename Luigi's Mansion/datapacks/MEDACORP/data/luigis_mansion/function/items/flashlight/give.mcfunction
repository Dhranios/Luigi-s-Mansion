execute store result score #temp ActionTime run clear @s *[minecraft:custom_data~{namespace:"luigis_mansion",id:"flashlight"}] 0
execute if score #temp ActionTime matches 0 run function luigis_mansion:entities/player/get_available_slot
execute if score #temp ActionTime matches 0 unless score @s AvailableSlot matches -1 run data modify storage luigis_mansion:data inventory set from entity @s Inventory
execute if score #temp ActionTime matches 0 unless score @s AvailableSlot matches -1 run data modify storage luigis_mansion:data inventory append value {Slot:0b,id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:unbreakable":{show_in_tooltip:0b},"minecraft:custom_model_data":0,"minecraft:item_name":'{"type":"translatable","translate":"luigis_mansion:item.flashlight"}',"minecraft:custom_data":{namespace:"luigis_mansion",id:"flashlight",is_nozzle:1b,model_data:{on:{components:{"minecraft:custom_model_data":0}},off:{components:{"minecraft:custom_model_data":1}}}}}}
execute if score #temp ActionTime matches 0 unless score @s AvailableSlot matches -1 if entity @s[tag=!flashlight] run data modify storage luigis_mansion:data inventory[-1] merge from storage luigis_mansion:data inventory[-1].components."minecraft:custom_data".model_data.off
execute if score #temp ActionTime matches 0 unless score @s AvailableSlot matches -1 store result storage luigis_mansion:data inventory[-1].Slot byte 1 run scoreboard players get @s AvailableSlot
execute if score #temp ActionTime matches 0 unless score @s AvailableSlot matches -1 run function luigis_mansion:other/sync_inventory
scoreboard players reset #temp ActionTime